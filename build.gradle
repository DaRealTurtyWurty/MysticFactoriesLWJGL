plugins {
    id 'java'
    id 'application'
}

ext {
    lwjglVersion = '3.3.6'
}

repositories {
    mavenCentral()
    maven {
        url "https://libraries.minecraft.net"
    }
}

dependencies {
    implementation platform("org.lwjgl:lwjgl-bom:$lwjglVersion")

    implementation "org.lwjgl:lwjgl"
    implementation "org.lwjgl:lwjgl-glfw"
    implementation "org.lwjgl:lwjgl-opengl"
    implementation "org.lwjgl:lwjgl-openal"
    implementation "org.lwjgl:lwjgl-stb"
    implementation "org.joml:joml:1.10.8"
    implementation "com.google.code.gson:gson:2.10.1"
    implementation "com.personthecat:fastnoise:0.10"
    implementation "de.articdive:jnoise-pipeline:4.1.0"
    implementation "com.mojang:datafixerupper:8.0.16"

    compileOnly "org.projectlombok:lombok:1.18.42"
    annotationProcessor "org.projectlombok:lombok:1.18.42"
}

static def getLwjglNatives() {
    def osName = System.getProperty("os.name").toLowerCase()
    def osArch = System.getProperty("os.arch").toLowerCase()

    if (osName.contains("windows")) {
        return "natives-windows"
    } else if (osName.contains("linux")) {
        return "natives-linux"
    } else if (osName.contains("mac") || osName.contains("osx")) {
        if (osArch.contains("aarch64") || osArch.contains("arm64")) {
            return "natives-macos-arm64"
        } else {
            return "natives-macos"
        }
    }
    return null
}

def lwjglNatives = getLwjglNatives()

if (lwjglNatives != null) {
    dependencies {
        runtimeOnly "org.lwjgl:lwjgl::${lwjglNatives}"
        runtimeOnly "org.lwjgl:lwjgl-glfw::${lwjglNatives}"
        runtimeOnly "org.lwjgl:lwjgl-opengl::${lwjglNatives}"
        runtimeOnly "org.lwjgl:lwjgl-openal::${lwjglNatives}"
        runtimeOnly "org.lwjgl:lwjgl-stb::${lwjglNatives}"
    }
}

application {
    // Default to client entrypoint; runClient and runServer tasks override this
    mainClass = 'dev.turtywurty.mysticfactories.client.ClientEntrypoint'
}

tasks.register('runClient', JavaExec) {
    group = 'application'
    description = 'Run the client'
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'dev.turtywurty.mysticfactories.client.ClientEntrypoint'
    standardInput = System.in
}

tasks.register('runServer', JavaExec) {
    group = 'application'
    description = 'Run the dedicated server'
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'dev.turtywurty.mysticfactories.server.ServerEntrypoint'
    standardInput = System.in
}
